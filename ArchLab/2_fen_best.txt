#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	iaddq $-9, %rdx
	jl rest		#SHOULD always taken 
				#since average MATTERS MORE than total

Loop:	mrmovq (%rdi), %r8	#load_adventure_here
	mrmovq 8(%rdi), %r9
	mrmovq 16(%rdi), %r10	#load_adventure_here
	mrmovq 24(%rdi), %r11
	mrmovq 32(%rdi), %r12	#load_adventure_here
	mrmovq 40(%rdi), %r13
	mrmovq 48(%rdi), %r14	#load_adventure_here
	mrmovq 56(%rdi), %rbx
	mrmovq 64(%rdi), %rcx
	
	
	rmmovq %r8, (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)
	rmmovq %r10, 16(%rsi)	# ...and store it to dst
	rmmovq %r11, 24(%rsi)
	rmmovq %r12, 32(%rsi)	# ...and store it to dst
	rmmovq %r13, 40(%rsi)
	rmmovq %r14, 48(%rsi)	# ...and store it to dst
	rmmovq %rbx, 56(%rsi)
	rmmovq %rcx, 64(%rsi)
	
	andq %r8, %r8
	jle Npos1_0		# 50/50, doesn't matter
	iaddq $1, %rax		# count++

Npos1_0:	
	andq %r9, %r9
	jle Npos1_1
	iaddq $1, %rax
	
Npos1_1:
	andq %r10, %r10
	jle Npos1_2
	iaddq $1, %rax
Npos1_2:
	andq %r11, %r11
	jle Npos1_3
	iaddq $1, %rax	

Npos1_3:	
	andq %r12, %r12
	jle Npos1_4
	iaddq $1, %rax
	
Npos1_4:
	andq %r13, %r13
	jle Npos1_5
	iaddq $1, %rax
	
Npos1_5:
	andq %r14, %r14
	jle Npos1_6
	iaddq $1, %rax
Npos1_6:
	andq %rbx, %rbx
	jle Npos1_7
	iaddq $1, %rax
Npos1_7:
	andq %rcx, %rcx
	jle update
	iaddq $1, %rax

update:
	iaddq $72, %rdi		
	iaddq $72, %rsi
test:
	iaddq $-9, %rdx
	jge Loop	#SHOULD always taken

rest:
	iaddq $9, %rdx
	jne Nposr0		#je->jne BEST optimization!!!
	ret



Nposr0:
    iaddq $-4, %rdx
    jl left123		#always taken(average > total)
    jg right5678
  
left4:
    mrmovq 24(%rdi), %r12
    mrmovq 16(%rdi), %r13
    mrmovq 8(%rdi), %r14
    mrmovq (%rdi), %rbx
    jmp npos4

left123:
    mrmovq (%rdi), %rbx
    iaddq $2, %rdx
    jl npos1
    mrmovq 8(%rdi), %r14
    je npos2
    mrmovq 16(%rdi), %r13
    jmp npos3

right5678:
    
    mrmovq 32(%rdi), %r11   #5
    mrmovq 24(%rdi), %r12   #4
    mrmovq 16(%rdi), %r13
    mrmovq 8(%rdi), %r14
    mrmovq (%rdi), %rbx
    iaddq $-2, %rdx
    jl npos5
    mrmovq 40(%rdi), %r10   #6
    je npos6
    mrmovq 56(%rdi), %r8    #8
    mrmovq 48(%rdi), %r9    #7

right78:
    iaddq $-1, %rdx
    je npos7


npos8:  #8
    andq %r8, %r8
    rmmovq %r8, 56(%rsi)
    jle npos7
    iaddq $1, %rax

npos7:  #7
    andq %r9, %r9
    rmmovq %r9, 48(%rsi)
    jle npos6
    iaddq $1, %rax

npos6:
    andq %r10, %r10
    rmmovq %r10, 40(%rsi)
    jle npos5
    iaddq $1, %rax

npos5:
    andq %r11, %r11
    rmmovq %r11, 32(%rsi)
    jle npos4
    iaddq $1, %rax

npos4:
    andq %r12, %r12
    rmmovq %r12, 24(%rsi)
    jle npos3
    iaddq $1, %rax
npos3:
    andq %r13, %r13
    rmmovq %r13, 16(%rsi)
    jle npos2
    iaddq $1, %rax
npos2:
    andq %r14, %r14
    rmmovq %r14, 8(%rsi)
    jle npos1
    iaddq $1, %rax

npos1:
    andq %rbx, %rbx
    rmmovq %rbx, (%rsi)
    jle done
    iaddq $1, %rax 
done:
	ret
	
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */